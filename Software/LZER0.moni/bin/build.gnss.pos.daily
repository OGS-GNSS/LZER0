#!/bin/tcsh
# Created by DZ (Nov 2018)
# modified by DZ (Nov 2018)
#
#********************  DEFAULTS ********************
set JOB		= `basename $0`
set CURDIR	= `pwd`
set path = ($path /usr/sbin /sbin /usr/local/bin /usr/bin /bin /home/${user}/bin .) #fundamental to run  scripts inside cron without full path commands
#
#********************  TUNING VARIABLES BEGIN ********************
#set DUMPDIR 		= /${HOME}/raw
set STORAGE 		= /mnt/hd
set DUMPDIR		= ${STORAGE}/gnss
set STORAGE_USE_LIM 	= 80 	#percent
set BINDIR   		= /home/lzer0/bin
#
# DEFAULT PARAMS
set SITE  	= "UNKN"
set YEAR  	= `date --date="1 day ago" +%Y`
set DOY   	= `date --date="1 day ago" +%j`
set FULLDATE 	= `date --date="1 day ago"  +%Y.%m.%d`
set FORCE = "N"
set PDAYS = ""

#********************  TUNING VARIABLES END  ********************
#
# check command line params. First one is used to set the site name, second one for brand
while($#)
        switch($1)
                case "-s":
			# Site name
			shift
			set SITE = `echo $1 | tr 'a-z' 'A-Z'`
			breaksw
		case "-y":
			# Year
                        shift
			set YEAR = $1
			breaksw
                case "-d":
                        # DOY
                        shift
                        set DOY = $1
                        breaksw
                case "-p":
                        #  Number of previous days to build
                        shift
			set PDAYS = $1
                        breaksw
                case "-f":
                        #  overwrite old file
                        set FORCE  = "Y"
                        breaksw
		default:
			shift
			breaksw
        endsw
        shift
end
@ tempDOY = $DOY - 1
set FULLDATE = `date -d "$tempDOY days ${YEAR}-01-01" +"%Y.%m.%d"`
set site = `echo $SITE | tr 'A-Z' 'a-z'`
set hourlyFileList = ""
if ($PDAYS == "") then
	if (-e ${DUMPDIR}/${YEAR}/${DOY} ) then
		if ( ! -e ${DUMPDIR}/${YEAR}/${DOY}/${site}.${FULLDATE}.${DOY}.pos.tar.gz) then
			echo "${DUMPDIR}/${YEAR}/${DOY}/${site}.${FULLDATE}.${DOY}.pos.tar.gz building."
			set hourlyFileList = `ls ${DUMPDIR}/${YEAR}/${DOY}/[a-x]/*.pos`
			if ("$hourlyFileList" != "") then
				cat `ls ${DUMPDIR}/${YEAR}/${DOY}/[a-x]/*.pos` > ${DUMPDIR}/${YEAR}/${DOY}/${site}.${FULLDATE}.${DOY}.pos
				tar -cvzf ${DUMPDIR}/${YEAR}/${DOY}/${site}.${FULLDATE}.${DOY}.pos.tar.gz -C ${DUMPDIR}/${YEAR}/${DOY} ${site}.${FULLDATE}.${DOY}.pos >& /dev/null
				rm -fR ${DUMPDIR}/${YEAR}/${DOY}/${site}.${FULLDATE}.${DOY}.pos
			else
				echo "No hourly pos file available"
			endif
		else
			if ($FORCE == "Y") then
				echo "${DUMPDIR}/${YEAR}/${DOY}/${site}.${FULLDATE}.${DOY}.pos.tar.gz building."
                        	set hourlyFileList = `ls ${DUMPDIR}/${YEAR}/${DOY}/[a-x]/*.pos`
				if ("$hourlyFileList" != "") then
					cat `ls ${DUMPDIR}/${YEAR}/${DOY}/[a-x]/*.pos` > ${DUMPDIR}/${YEAR}/${DOY}/${site}.${FULLDATE}.${DOY}.pos
        	                	tar -cvzf ${DUMPDIR}/${YEAR}/${DOY}/${site}.${FULLDATE}.${DOY}.pos.tar.gz -C ${DUMPDIR}/${YEAR}/${DOY} ${site}.${FULLDATE}.${DOY}.pos >& /dev/null
                	        	rm -fR ${DUMPDIR}/${YEAR}/${DOY}/${site}.${FULLDATE}.${DOY}.pos
				else
					echo "No hourly pos file available"
				endif
			else
				echo "${DUMPDIR}/${YEAR}/${DOY}/${site}.${FULLDATE}.${DOY}.pos.tar.gz already exists."
			endif
		endif
	else
		echo "${DUMPDIR}/${YEAR}/${DOY} empty directory or not present on disk"
	endif
else
	foreach PDAY (`seq $PDAYS -1 1`)
		set CurrDoy   = `date --date="$PDAY day ago" +%j`
		set CurrYear  = `date --date="$PDAY day ago" +%Y`
		if ($FORCE == "Y") then
			build.gnss.pos.daily -s $SITE -y $CurrYear -d $CurrDoy -f
		else
			build.gnss.pos.daily -s $SITE -y $CurrYear -d $CurrDoy
		endif
	end
endif
